apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'
// enable generation of querydsl query types
apply plugin: "com.ewerk.gradle.plugins.querydsl"

description = 'AppManager domain - contains domain model'

buildscript {
  repositories {
  	mavenLocal()
    jcenter()
  }
  dependencies {
    classpath 'com.ewerk.gradle.plugins:querydsl-plugin:1.0.3'
  }
}

repositories {
	mavenLocal()
	mavenCentral()
	maven { url 'http://repository.jboss.org/nexus/content/groups/public' }
	maven { url 'http://repository.jboss.org/nexus/content/repositories/deprecated' }
	maven { url 'http://repo.jfrog.org/artifactory/repo' }
}

ext.libraryVersions = [
	javaee                  : '7.0',
	wildfly                 : '8.2.0.Final',
	glassfish				: '4.0',
	junit                   : '4.11',
	arquillian              : '1.1.5.Final',
	arquillianPersistence	: '1.0.0.Alpha7',
	shrinkWrapResolverGradle: '2.1.0',
	hibernateVersion		: '4.3.5.Final'
	querydsl				: '2.6.0'
]

configurations {
	apt { description 'for annotation processors' }
	provided

	glassfishEmbeddedTestRuntime { extendsFrom testRuntime }
	jbossEmbeddedTestRuntime { extendsFrom testRuntime, provided }
	jbossRemoteTestRuntime { extendsFrom testRuntime }
}

dependencies {
	compile project(':xmdlab-framework-jee')
	
	compile 'org.slf4j:slf4j-simple:1.7.7'
	compile "javax:javaee-api:${libraryVersions.javaee}"

	testCompile "junit:junit:${libraryVersions.junit}"
	testCompile "org.hamcrest:hamcrest-library:1.3"

	testRuntime "com.h2database:h2:1.4.177"

	//apt "org.hibernate:hibernate-jpamodelgen:${libraryVersions.hibernateVersion}"
	compile "com.mysema.querydsl:querydsl-jpa:$libraryVersions.querydsl"

	// Spring data
	compile 'org.springframework.data:spring-data-jpa:1.8.0.M1'

	// arquillian persistence extension
	testCompile "org.jboss.arquillian.extension:arquillian-persistence-api:${libraryVersions.arquillianPersistence}"
	testCompile "org.jboss.arquillian.extension:arquillian-persistence-core:${libraryVersions.arquillianPersistence}"
	testCompile "org.jboss.arquillian.extension:arquillian-persistence-spi:${libraryVersions.arquillianPersistence}"
	testCompile "org.jboss.arquillian.extension:arquillian-persistence-dbunit:${libraryVersions.arquillianPersistence}"

	// jboss arquilian / shrinkwrap
	testCompile "org.jboss.arquillian.junit:arquillian-junit-container:${libraryVersions.arquillian}"
	testCompile "org.jboss.arquillian.protocol:arquillian-protocol-servlet:${libraryVersions.arquillian}"

	testCompile "org.jboss.shrinkwrap.resolver:shrinkwrap-resolver-depchain:${libraryVersions.shrinkWrapResolverGradle}"
	testCompile "org.jboss.shrinkwrap.resolver:shrinkwrap-resolver-gradle-depchain:2.2.0-alpha-2"

	jbossEmbeddedTestRuntime "org.wildfly:wildfly-arquillian-container-managed:${libraryVersions.wildfly}"

	jbossRemoteTestRuntime "org.wildfly:wildfly-arquillian-container-remote:${libraryVersions.wildfly}"

	glassfishEmbeddedTestRuntime group: 'org.jboss.arquillian.container', name: 'arquillian-glassfish-embedded-3.1', version: '1.0.0.CR4'
	glassfishEmbeddedTestRuntime group: 'org.glassfish.main.extras', name: 'glassfish-embedded-all', version: libraryVersions.glassfish
}

// querydsl configuration
querydsl {
  jpa = true
  querydslSourcesDir = 'build/querydsl/java'
}

sourceSets.main.java.srcDir 'build/querydsl/java'

// jbossEmbeddedTest
task jbossEmbeddedTest(type: Test)

jbossEmbeddedTest {
	include '**/integration/*'

	systemProperty 'arquillian.launch', "jbossas-embedded"
	systemProperty 'java.util.logging.manager', 'org.jboss.logmanager.LogManager'
}

// jbossRemoteTest
task jbossRemoteTest(type: Test)

jbossRemoteTest {
	include '**/integration/*'

	systemProperty 'arquillian.launch', "jbossas-remote"
}

// glassfishEmbeddedTest
task glassfishEmbeddedTest(type: Test) {
	include '**/integration/*'

	systemProperty 'arquillian.launch', "glassfish-embedded"
	systemProperty 'java.util.logging.config.file', "${projectDir}/src/test/resources-glassfish-embedded/logging.properties"
	systemProperty 'derby.stream.error.file', '${projectDir}/build/derby.log'
}

// JPA Model Gen
def generatedJavaSrcDir = "build/generated-sources/main/java"
sourceSets.main.java.srcDirs += [generatedJavaSrcDir]

task procJava(type: JavaCompile, group: "build", description: "Annotation processing") {
	def aptDestDir = new File(project.projectDir, generatedJavaSrcDir)
	source = project.sourceSets.main.java.srcDirs.findAll {
		!it.equals(aptDestDir)
	}

	classpath = project.configurations.compile
	options.compilerArgs.addAll "-proc:only", "-implicit:none",
			"-processorpath", project.configurations.apt.asPath
	destinationDir = aptDestDir
}
compileJava.dependsOn procJava

sourceSets {
	test {
		resources { srcDir 'src/test/resources' }
		resources { srcDir 'src/test/resources-jbossas-embedded' }

		compileClasspath += main.output + configurations.provided + main.compileClasspath
		runtimeClasspath += main.output + configurations.provided + main.compileClasspath + main.runtimeClasspath
	}
}

tasks.withType(Test).matching({ t-> t.name.endsWith('Test') } as Spec).each { t ->

	if (System.getProperty('test.debug', 'false') == 'true') {
		t.jvmArgs '-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=5005'
	}
	if (System.getProperty('test.single', '') != '') {
		t.includes = [
			System.getProperty('test.single')
		]
	}
	t.testLogging.showStandardStreams = true
	t.testClassesDir = project.sourceSets.test.output.classesDir
	t.classpath = project.configurations.getByName(t.name + 'Runtime') +
			project.sourceSets.main.output +
			project.sourceSets.test.output
	if (t.name.startsWith('jbossEmbedded')) {
		t.classpath += files('src/test/resources-jbossas-embedded')
	} else if(t.name.startsWith('glassfishEmbedded')){
		t.classpath += files('src/test/resources-glassfish-embedded')
	}
}

test {
	exclude '**/integration/*'
	testLogging.showStandardStreams = true

	jvmArgs '-XX:MaxPermSize=256m'
}

eclipse {
	classpath {
		//you can tweak the classpath of the Eclipse project by adding extra configurations:
		plusConfigurations += [
			configurations.jbossEmbeddedTestRuntime
		]

		//default settings for downloading sources and Javadoc:
		downloadSources = true
		downloadJavadoc = false
	}
}