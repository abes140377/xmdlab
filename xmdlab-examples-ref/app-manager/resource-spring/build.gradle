description = 'AppManager resource spring - contains spring resources'

buildscript {
	repositories {
		jcenter()
	}

	dependencies {
		classpath 'com.bmuschko:gradle-cargo-plugin:2.0.3'
	}
}

apply plugin: 'war'
apply plugin: 'com.bmuschko.cargo'

configurations {
	provided
	glassfishEmbeddedTestRuntime { extendsFrom testRuntime }
	jbossEmbeddedTestRuntime { extendsFrom testRuntime, provided }
	jbossRemoteTestRuntime { extendsFrom testRuntime }
}

sourceSets {
	test {
		resources { srcDir 'src/test/resources' }
		resources { srcDir 'src/test/resources-jbossas-embedded' }
	}
}

dependencies {
	//compile project(':domain-sql')
	//compile project(':service')
	
	providedCompile "javax:javaee-api:${libraryVersions.javaeeVersion}"

	compile "org.slf4j:slf4j-simple:${libraryVersions.slf4jVersion}"
	
	compile "org.springframework:spring-webmvc:3.2.4.RELEASE"
	compile "org.springframework.data:spring-data-rest-webmvc:1.0.0.RELEASE"

	testCompile "junit:junit:${libraryVersions.junitVersion}"
	testCompile "org.hamcrest:hamcrest-library:${libraryVersions.hamcrestVersion}"
		
	// jboss arquilian / shrinkwrap
	testCompile "org.jboss.arquillian.junit:arquillian-junit-container:${libraryVersions.arquillianVersion}"
	testCompile "org.jboss.arquillian.protocol:arquillian-protocol-servlet:${libraryVersions.arquillianVersion}"

	testCompile "org.jboss.shrinkwrap.resolver:shrinkwrap-resolver-depchain:${libraryVersions.shrinkWrapResolverGradleVersion}"
	testCompile "org.jboss.shrinkwrap.resolver:shrinkwrap-resolver-gradle-depchain:${libraryVersions.shrinkwrapResolverGradleDepchainVersion}"

	// wildfly managed
	jbossEmbeddedTestRuntime "org.wildfly:wildfly-arquillian-container-managed:${libraryVersions.wildflyVersion}"

	// wildfly remote
	jbossRemoteTestRuntime "org.wildfly:wildfly-arquillian-container-remote:${libraryVersions.wildflyVersion}"

	// glassfish managed
	glassfishEmbeddedTestRuntime "org.jboss.arquillian.container:arquillian-glassfish-embedded-3.1:${libraryVersions.arquillianGlassfishEmbeddedVersion}"
	glassfishEmbeddedTestRuntime "org.glassfish.main.extras:glassfish-embedded-all:${libraryVersions.glassfishVersion}"
	
	// cargo
	cargo 'org.wildfly:wildfly-controller-client:8.1.0.Final'
	cargo 'org.codehaus.cargo:cargo-ant:1.4.8'
}

// jbossEmbeddedTest
task jbossEmbeddedTest(type: Test)

jbossEmbeddedTest {
	include '**/integration/*'

	systemProperty 'arquillian.launch', "jbossas-embedded"
	systemProperty 'java.util.logging.manager', 'org.jboss.logmanager.LogManager'
}

// jbossRemoteTest
task jbossRemoteTest(type: Test)

jbossRemoteTest {
	include '**/integration/*'

	systemProperty 'arquillian.launch', "jbossas-remote"
}

// glassfishEmbeddedTest
task glassfishEmbeddedTest(type: Test) {
	include '**/integration/*'

	systemProperty 'arquillian.launch', "glassfish-embedded"
	systemProperty 'java.util.logging.config.file', "${projectDir}/src/test/resources-glassfish-embedded/logging.properties"
	systemProperty 'derby.stream.error.file', "${projectDir}/build/derby.log"
}

tasks.withType(Test).matching({ t-> t.name.endsWith('Test') } as Spec).each { t ->

	if (System.getProperty('test.debug', 'false') == 'true') {
		t.jvmArgs '-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=5005'
	}
	if (System.getProperty('test.single', '') != '') {
		t.includes = [
			System.getProperty('test.single')
		]
	}
	t.testLogging.showStandardStreams = true
	t.testClassesDir = project.sourceSets.test.output.classesDir
	t.classpath = project.configurations.getByName(t.name + 'Runtime') +
			project.sourceSets.main.output +
			project.sourceSets.test.output
	if (t.name.startsWith('jbossEmbedded')) {
		t.classpath += files('src/test/resources-jbossas-embedded')
	} else if(t.name.startsWith('glassfishEmbedded')){
		t.classpath += files('src/test/resources-glassfish-embedded')
	}
}

test {
	exclude '**/integration/*'
	testLogging.showStandardStreams = true

	jvmArgs '-XX:MaxPermSize=256m'
	
	systemProperty 'slf4jVersion', libraryVersions.slf4jVersion
}

cargo {
    containerId = 'wildfly8x'
	
	deployable {
		file = file('build/libs/resource-spring-0.1.0-SNAPSHOT.war')
		context = 'app-manager-resource-spring'
	}
	
    local {
        homeDir = file(System.getenv('JBOSS_HOME'))
    }
    remote {
        hostname = '127.0.0.1'
        username = 'seba'
        password = '00106078'
    }
}

eclipse {
	project {
		natures = [
			'org.springsource.ide.eclipse.gradle.core.nature',
			'org.eclipse.jdt.core.javanature'
		]
	}
	classpath {
		//default settings for downloading sources and Javadoc:
		downloadSources = true
		downloadJavadoc = true
	}
}